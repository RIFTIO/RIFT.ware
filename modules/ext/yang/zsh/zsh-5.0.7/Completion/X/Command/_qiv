#compdef qiv

_arguments -s \
  '(- *)'{-h,--help}'[display help information]' \
  '(- *)'{-v,--version}'[display version information]' \
  '--display:display:_x_display' \
  \*{-F,--file}'[read file names from file]:file:_files' \
  '(-e --center)'{-e,--center}'[disable window centering]' \
  '(-w --fixed_width -W --fixed_zoom)'{-w,--fixed_width}'[use window with specified width]:width' \
  '(-w --fixed_width -W --fixed_zoom)'{-W,--fixed_zoom}'[use window with specified zoom factor]:zoom (percentage)' \
  '(-x -y -z --root --root_t --root_s -f --file *)'{-x,--root}'[use image as the current desktop background, centered]:image file:_files' \
  '(-x -y -z --root --root_t --root_s -f --file *)'{-y,--root_t}'[use image on the current desktop background, tiled]:image file:_files' \
  '(-x -y -z --root --root_t --root_s -f --file *)'{-z,--root_s}'[use image as the current desktop background, stretched]:image file:_files' \
  '(-m --maxpect)'{-m,--maxpect}'[expand image to fit screen size]' \
  '(-t --scale_down)'{-t,--scale_down}'[shrink images to fit screen size]' \
  '(-b --brightness)'{-b,--brightness}'[set brightness]:brightness (-32..32)' \
  '(-c --contrast)'{-c,--contrast}'[set contrast]:contrast (-32..32)' \
  '(-g --gamma)'{-g,--gamma}'[set gamma]:gamma (-32..32)' \
  '(-n --no_filter)'{-n,--no_filter}'[disable filtering of images by extension]' \
  '(-i --no_statusbar -I --statusbar)'{-i,--no_statusbar}'[disable statusbar]' \
  '(-i --no_statusbar -I --statusbar)'{-I,--statusbar}'[enable statusbar]' \
  '(-p --transparency)'{-p,--transparency}'[enable transparency]' \
  '(-a --do_grab)'{-a,--do_grab}'[grab the pointer in windowed mode]' \
  '(-G --disable_grab)'{-G,--disable_grab}'[disable pointer grabbing]' \
  '(-o --bg_color)'{-o,--bg_color}'[set root background color]:color:_x_color' \
  '(-s --slide)'{-s,--slide}'[start slideshow immediately]' \
  '(-r --random)'{-r,--random}'[shuffle images for slideshow]' \
  '(-R --readonly)'{-R,--readonly}'[disable delete feature]' \
  '(-S --shuffle)'{-S,--shuffle}'[shuffle images for next/prev feature]' \
  '(-d --delay)'{-d,--delay}'[set slideshow delay]:delay (seconds)' \
  '(-f --fullscreen)'{-f,--fullscreen}'[use fullscreen window on startup]' \
  '(-P --ignore_path_sort)'{-P,--ignore_path_sort}'[sort images by just the filename]' \
  '(-M --merged_case_sort)'{-M,--merged_case_sort}'[sort images case-insensitively]' \
  '(-N --numeric_sort)'{-N,--numeric_sort}'[numeric image sort]' \
  '(-T --watch)'{-T,--watch}'[reload the image if it changes]' \
  '*:image file:_files -g "*.(#i)(bmp|eim|gif|jp([eg]|eg)|pcx|png|p[pngb]m|tif(|f)|tga|x[pb]m)(-.)"'
